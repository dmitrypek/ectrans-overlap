! (C) Copyright 2000- ECMWF.
! (C) Copyright 2000- Meteo-France.
! 
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.
!

MODULE FTDIR_CTL_MOD

USE PARKIND1, ONLY: JPRB
USE DIR_TRANS_CTL_MOD, ONLY : ZGTF,IREQ_RECV

IMPLICIT NONE

CONTAINS
SUBROUTINE FTDIR_CTL_COMM_SEND(NSTRFS,KF_UV_G,KF_SCALARS_G,KF_GP,KF_FS, &
 & KVSETUV,KVSETSC,KPTRGP,&
 & KVSETSC3A,KVSETSC3B,KVSETSC2, IREQ_RECV, IBLK,&
 & PGP,PGPUV,PGP3A,PGP3B,PGP2)

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE TPM_GEN   ,ONLY : NSTACK_MEMORY_TR
USE TPM_TRANS       ,ONLY : FOUBUF_IN
USE TPM_DISTR       ,ONLY : D, MYPROC, NPROC
USE TRGTOL_MOD      ,ONLY : TRGTOL
USE FOURIER_OUT_MOD ,ONLY : FOURIER_OUT
USE FTDIR_MOD       ,ONLY : FTDIR

! Dummy arguments

INTEGER(KIND=JPIM),INTENT(IN) :: KF_UV_G,KF_SCALARS_G,KF_GP,KF_FS
INTEGER(KIND=JPIM) ,OPTIONAL, INTENT(IN) :: KVSETUV(:)
INTEGER(KIND=JPIM) ,OPTIONAL, INTENT(IN) :: KVSETSC(:)
INTEGER(KIND=JPIM) ,OPTIONAL, INTENT(IN) :: KPTRGP(:)
INTEGER(KIND=JPIM) ,OPTIONAL, INTENT(IN) :: KVSETSC3A(:)
INTEGER(KIND=JPIM) ,OPTIONAL, INTENT(IN) :: KVSETSC3B(:)
INTEGER(KIND=JPIM) ,OPTIONAL, INTENT(IN) :: KVSETSC2(:)
INTEGER(KIND=JPIM) ,OPTIONAL, INTENT(IN) :: IREQ_RECV(:)
INTEGER(KIND=JPIM) ,OPTIONAL, INTENT(IN) :: NSTRFS
REAL(KIND=JPRB),OPTIONAL    , INTENT(IN) :: PGP(:,:,:)
REAL(KIND=JPRB),OPTIONAL    , INTENT(IN) :: PGPUV(:,:,:,:)
REAL(KIND=JPRB),OPTIONAL    , INTENT(IN) :: PGP3A(:,:,:,:)
REAL(KIND=JPRB),OPTIONAL    , INTENT(IN) :: PGP3B(:,:,:,:)
REAL(KIND=JPRB),OPTIONAL    , INTENT(IN) :: PGP2(:,:,:)
INTEGER(KIND=JPIM), INTENT(IN) :: IBLK

INTEGER(KIND=JPIM) :: IST
INTEGER(KIND=JPIM) :: IVSETUV(KF_UV_G)
INTEGER(KIND=JPIM) :: IVSETSC(KF_SCALARS_G)
INTEGER(KIND=JPIM) :: IVSET(KF_GP)
INTEGER(KIND=JPIM) :: IFGP2,IFGP3A,IFGP3B,IOFF,J3

!     ------------------------------------------------------------------

! Field distribution in Spectral/Fourier space

IF(PRESENT(KVSETUV)) THEN
  IVSETUV(:) = KVSETUV(:)
ELSE
  IVSETUV(:) = -1
ENDIF
IVSETSC(:) = -1
IF(PRESENT(KVSETSC)) THEN
  IVSETSC(:) = KVSETSC(:)
ELSE
  IOFF=0
  IF(PRESENT(KVSETSC2)) THEN
    IFGP2=UBOUND(KVSETSC2,1)
    IVSETSC(1:IFGP2)=KVSETSC2(:)
    IOFF=IOFF+IFGP2
  ENDIF
  IF(PRESENT(KVSETSC3A)) THEN
    IFGP3A=UBOUND(KVSETSC3A,1)
    DO J3=1,UBOUND(PGP3A,3)
      IVSETSC(IOFF+1:IOFF+IFGP3A)=KVSETSC3A(:)
      IOFF=IOFF+IFGP3A
    ENDDO
  ENDIF
  IF(PRESENT(KVSETSC3B)) THEN
    IFGP3B=UBOUND(KVSETSC3B,1)
    DO J3=1,UBOUND(PGP3B,3)
      IVSETSC(IOFF+1:IOFF+IFGP3B)=KVSETSC3B(:)
      IOFF=IOFF+IFGP3B
    ENDDO
  ENDIF
ENDIF

IST = 1
IF(KF_UV_G > 0) THEN
  IVSET(IST:IST+KF_UV_G-1) = IVSETUV(:)
  IST = IST+KF_UV_G
  IVSET(IST:IST+KF_UV_G-1) = IVSETUV(:)
  IST = IST+KF_UV_G
ENDIF
IF(KF_SCALARS_G > 0) THEN
  IVSET(IST:IST+KF_SCALARS_G-1) = IVSETSC(:)
  IST = IST+KF_SCALARS_G
ENDIF


! Transposition

CALL GSTATS(158,0)
CALL TRGTOL(ZGTF(NSTRFS,1),KF_FS,KF_GP,KF_SCALARS_G,IVSET,KPTRGP,&
 IREQ_RECV,IBLK,PGP,PGPUV,PGP3A,PGP3B,PGP2)
CALL GSTATS(158,1)
CALL GSTATS(106,0)

END SUBROUTINE FTDIR_CTL_COMM_SEND

SUBROUTINE FTDIR_CTL_COMP(KF_FS,NSTRFS)

USE PARKIND1,        ONLY: JPIM, JPRB
USE TPM_TRANS,       ONLY: FOUBUF_IN
USE FOURIER_OUT_MOD, ONLY: FOURIER_OUT
USE FTDIR_MOD,       ONLY: FTDIR
USE TPM_DISTR,       ONLY: D

! Dummy arguments

INTEGER(KIND=JPIM), INTENT(IN) :: KF_FS,NSTRFS

INTEGER(KIND=JPIM) :: JGL, IBLEN

!     ------------------------------------------------------------------

! Fourier transform

IBLEN=D%NLENGT0B*2*KF_FS
IF (ALLOCATED(FOUBUF_IN)) THEN
  IF (MAX(1,IBLEN) > SIZE(FOUBUF_IN)) THEN
    DEALLOCATE(FOUBUF_IN)
    ALLOCATE(FOUBUF_IN(MAX(1,IBLEN)))
  ENDIF
ELSE
  ALLOCATE(FOUBUF_IN(MAX(1,IBLEN)))
ENDIF

CALL GSTATS(1640, 0)
! If this rank has any Fourier fields, Fourier transform them
IF (KF_FS > 0) THEN
  ! Loop over latitudes
  !$OMP PARALLEL DO SCHEDULE(DYNAMIC,1) PRIVATE(JGL)
  DO JGL = 1, D%NDGL_FS
    ! Fourier transform
    CALL FTDIR(ZGTF(NSTRFS,1), KF_FS, JGL)

    ! Save Fourier data in FOUBUF_IN
    CALL FOURIER_OUT(ZGTF(NSTRFS,1), KF_FS, JGL)
  ENDDO
  !$OMP END PARALLEL DO
ENDIF
CALL GSTATS(1640, 1)

CALL GSTATS(106,1)

!DEALLOCATE(ZGTF)

!     ------------------------------------------------------------------

END SUBROUTINE FTDIR_CTL_COMP

END MODULE FTDIR_CTL_MOD



